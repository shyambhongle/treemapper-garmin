import Toybox.Application;
import Toybox.Lang;
import Toybox.WatchUi;

//! TreeMapper GPS for Garmin.
//!
//! A high accuracy GPS source for the TreeMapper phone app. The watch holds the
//! receiver, the phone holds everything else: species, measurements, photos,
//! auth, upload. See DESIGN.md.
//!
//! Session model: the receiver starts with the app and stops with it. The user
//! opens this at the plot and leaves it running, so every request, whether from
//! the phone modal or the watch button, is answered from an already converged
//! fix. See GpsService for why that matters.
class TreeMapperApp extends Application.AppBase {

    function initialize() {
        AppBase.initialize();
    }

    //! Start the receiver and the phone link before any view exists, so the app
    //! is useful the moment it is on screen. When the phone launches this
    //! remotely with openApplication(), this is the first thing that runs.
    function onStart(state as Dictionary?) as Void {
        var session = new SessionState();
        $.gSession = session;
        session.startSession();
    }

    //! Release the receiver. Connect IQ does not do this for us, and a GNSS
    //! receiver left running is the difference between a working day and an
    //! afternoon.
    function onStop(state as Dictionary?) as Void {
        var session = $.gSession;
        if (session != null) {
            session.endSession();
        }
        $.gSession = null;
    }

    function getInitialView() as [WatchUi.Views] or [WatchUi.Views, WatchUi.InputDelegates] {
        return [new HomeView(), new HomeDelegate()];
    }
}
